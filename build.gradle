plugins {
    id 'java'
    id 'application'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'maven-publish'
}

group = 'fr.sandro642.orbit'
version = '0.2.0'

task generateVersionClass {
    doLast {
        def versionFile = file("$buildDir/generated/source/version/fr/sandro642/orbit/update/Version.java")
        versionFile.parentFile.mkdirs()
        versionFile.text = """
package fr.sandro642.orbit.update;

public class Version {
    public static final String VERSION = "${project.version}";
}
"""
    }
}

compileJava.dependsOn generateVersionClass
sourceSets.main.java.srcDir "$buildDir/generated/source/version"

tasks.register('printVersion') {
    doLast {
        println project.version
    }
}

shadowJar {
    archiveClassifier.set('')


    manifest {
        attributes 'Main-Class': 'fr.sandro642.orbit.Orbit'
    }
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    implementation 'io.projectreactor:reactor-core:3.6.9'
    implementation 'com.google.code.gson:gson:2.10.1'
    implementation 'org.eclipse.jgit:org.eclipse.jgit:6.10.0.202406032230-r'
    implementation 'ch.qos.logback:logback-classic:1.4.14'
}

application {
    mainClass = 'fr.sandro642.orbit.Orbit'
}

tasks.jar {
    manifest {
        attributes 'Main-Class': 'fr.sandro642.orbit.Orbit'
    }
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

tasks.test {
    useJUnitPlatform()
}

test {
    useJUnitPlatform()
}

java {
    withSourcesJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            groupId = project.group.toString()
            artifactId = 'Orbit'
            version = project.version.toString()
            pom {
                name = 'Orbit'
                description = 'Everything revolves around you. Trading has never been simpler, at least on the desktop.'
                url = 'https://sandro642.github.io/Orbit/'
                licenses {
                    license {
                        name = 'GPL-3.0'
                        url = 'https://github.com/Sandro642/Orbit/blob/main/LICENSE'
                    }
                }
                developers {
                    developer {
                        id = 'sandro642'
                        name = 'Sandro'
                        email = 'sandro33810@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/sandro642/Orbit.git'
                    developerConnection = 'scm:git:ssh://github.com/sandro642/Orbit.git'
                    url = 'https://github.com/sandro642/Orbit'
                }
            }
        }
    }
    repositories {
        maven {
            name = 'LocalRepo'
            url = uri(layout.buildDirectory.dir('repo'))
        }
    }
}